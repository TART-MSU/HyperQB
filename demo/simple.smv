
-- 1  high := {true, false}
-- 2  low := false
-- 3  if (high):
-- 4    low := true;
-- 5  halt
MODULE main
 VAR
    high: boolean;
    low:  boolean;
    PC: 1..5;
    halt: boolean;
 ASSIGN
    -- Initial Conditions, here are dummy values.
    init(high):= FALSE;
    init(low) := FALSE;
    init(halt):= FALSE;
    init(PC):= 1; -- but program counter starts at 1.

    -- Transition Relations
    next(high) :=
      case
        (PC=1): {TRUE, FALSE}; -- on line 1, high := {True, False}
        TRUE: high;
      esac;
    next(low) :=
      case
        (PC=2): FALSE; -- on line 2, low is set to false
        (PC=4): TRUE; -- on line 4, low is set to true
        TRUE: low;
      esac;
    next(PC) :=
      case
        (PC=1): 2;
        (PC=2): 3;
        (PC=3 & high): 4; -- the if-the-else on line 3
        (PC=3 & !high): 5; -- the if-the-else on line 3
        (PC=4): 5;
        (PC=5): 5;
        TRUE: PC;
      esac;
    next(halt) :=
      case
        (PC=5): TRUE;
        TRUE: halt;
      esac;
